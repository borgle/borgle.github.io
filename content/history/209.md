Title: ACCESS数据库备份的asp代码
Date: 2006-12-19 11:39
Modified: 2006-12-19 11:39
Category: Program
Tags: db,ACCESS
Slug: 209
Authors: Yoker.Wu
Summary: 
    下面的代码是很久前写的了，在分析dvbbs中ACCESS数据库备份时，写的了，有用就拿去吧，反正代码共享。。
    ```
    <html>
    <head>
    <meta name="robots" content="all" />
    <meta content="Yoker.Wu" name="author" />
    <meta content="Copyright (c) 2005 " name="Copyright" />
    <title>数据库备份与压缩</title>
    <meta http-equiv="Content-Type" content="text/html; charset=gb2312" />
    ```


下面的代码是很久前写的了，在分析dvbbs中ACCESS数据库备份时，写的了，有用就拿去吧，反正代码共享。。

```
<html>
<head>
<meta name="robots" content="all" />
<meta content="Yoker.Wu" name="author" />
<meta content="Copyright (c) 2005 " name="Copyright" />
<title>数据库备份与压缩</title>
<meta http-equiv="Content-Type" content="text/html; charset=gb2312" />
<meta name="MSSmartTagsPreventParsing" content="TRUE" />
<meta http-equiv="MSThemeCompatible" content="Yes" />
<style type="text/css">
<!--
	* {font-size:12px;}
	body {line-height:150%}
	form * {padding:1px;}
-->
</style>
<script type="text/javascript">
<!--
function ChangeOPT(obj){
	var b=document.getElementById('DBPath').style;
	var s=document.getElementById('DBName').style;
	switch (obj.options[obj.selectedIndex].value)
	{
	case 'Compact' :
		b.display = '' ; s.display = 'none';break;
	case 'DeleteBackup' :
		b.display = 'none' ; s.display = '';break;
	case 'Backup' :
		b.display = ''; s.display = '';
	case 'Compact&Backup' :
		b.display = ''; s.display = '';
	}
}
-->
</script>
</head>

<body>
<%
Select Case Request.form("opt")
	Case "Compact" : Call CompactDatabase()
	Case "Backup" : Call BackUpDatabase()
	Case "DeleteBackup" : Call DeleteBackupDatabase()
	Case "Compact&Backup" : CompactDatabase : BackUpDatabase
End Select
%>
<form action="?" method="post" name="theform">
数据库操作方式：<select style="width:200px" name="opt" onChange="ChangeOPT(this)"><option value="DeleteBackup">删除备份数据库</option><option value="Compact">压缩数据库</option><option value="Backup">备份数据库</option><option selected=selected value="Compact&Backup">压缩并备份数据库</option></select><br />
<span id="DBPath">数据库相对路径：<input name="DataBasePath" size="48" type="text" value="Data.mdb" />（当前位置：<%=server.mappath(Request.ServerVariables("Script_Name"))%>）<br /></span>
<span id="DBName">备份数据库路径：<input type="text" name="BackDBPath" size="48" value="Back_Data.mdb" /><br /></span>
<input type="submit" value=" 开始操作 " />
</form>
</body>
</html>
<%
Sub CompactDatabase()
	'On Error Resume Next
	checkLogin
    Dim TargetDB,ResourceDB,DataBasePath
    Dim fso,Jet_Conn,oJetEngine,ret
    Jet_Conn = "Provider=Microsoft.Jet.OLEDB.4.0; Data source="
    Set fso= server.CreateObject("Scripting.FileSystemObject")
    Set oJetEngine = Server.CreateObject("JRO.JetEngine")
	DataBasePath = Request.Form("DataBasePath")
    ResourceDB = Server.MapPath(DataBasePath)
	If fso.FileExists(ResourceDB) Then
		'处理以前可能出错的文件
		TargetDB = Server.MapPath(DataBasePath&".bak")
		If fso.FileExists(TargetDB) Then
			fso.DeleteFile(TargetDB)
		End If
		oJetEngine.CompactDatabase Jet_Conn&ResourceDB,Jet_Conn&TargetDB
		fso.DeleteFile ResourceDB
		fso.MoveFile TargetDB,ResourceDB
		If err Then
			Err.clear
			ret = false
		Else
			ret = true
		End If
	End If
    Set fso=Nothing
    Set oJetEngine=Nothing
	If ret Then
		Response.write "<li>压缩成功</li>"
	Else
		Response.write "<li>压缩出现错误，请重新压缩.</li>"
	End If
End Sub

Sub BackUpDatabase()
	On Error Resume Next
	checkLogin
	Dim DataBasePath,fso,BackDBPath
	DataBasePath = Request.Form("DataBasePath")
	BackDBPath = Request.Form("BackDBPath")
    Set fso= server.CreateObject("Scripting.FileSystemObject")
	fso.CopyFile Server.Mappath(DataBasePath),server.mappath(BackDBPath)
	Set fso = Nothing
	If Err Then
		Err.clear
		Response.write "<li>出现错误，备份失败！请检查你输入的目录是否存在,或者文件已经存在</li>"
	Else
		Response.write "<li>备份数据库成功</li>"
	End If
End Sub

Sub DeleteBackupDatabase()
	On Error Resume Next
	checkLogin
	Dim DataBasePath,fso,BackDBPath
	BackDBPath = Request.Form("BackDBPath")
    Set fso= CreateObject("Scripting.FileSystemObject")
	If fso.FileExists(Server.Mappath(BackDBPath)) Then
		fso.deleteFile Server.Mappath(BackDBPath)
	End If
	Set fso = Nothing
	If Err Then
		response.write "<li>删除不成功！</li>"
	Else
		response.write "<li>删除成功</li>"
	End If
End Sub

Sub checkLogin()
	If session("loginid")<>"true" Then
		'Response.write "<li>请登陆才可以管理</li>"
		'response.end
	End If
End Sub
%>
```
